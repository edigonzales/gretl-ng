allprojects {
    buildscript {
        repositories {
            flatDir {
                dirs '/home/gradle/libs'
            }
        }
        dependencies {
            classpath fileTree(dir: '/home/gradle/libs', include: '*.jar')
        }
    }
}

// Notwendig, falls mit Plugin-DSL-Syntax gearbeitet wird.
settingsEvaluated { settings ->
    settings.pluginManagement {
        plugins {
            id 'ch.so.agi.gretl' version 'latest.integration'
            id "org.hidetake.ssh" version "latest.integration"
            id "de.undercouch.download" version "latest.integration"
        }
        resolutionStrategy {
            eachPlugin {
                if (requested.id.namespace == 'ch.so.agi') {
                    useModule('ch.so.agi:gretl:latest.integration')
                } else if (requested.id.namespace == 'org.hidetake') {
                    useModule('org.hidetake:gradle-ssh-plugin:latest.integration')
                } else if (requested.id.namespace == 'de.undercouch') {
                    useModule('de.undercouch:gradle-download-task:latest.integration')
                }
            }
        }
        // Zwingend, da sonst die Plugins heruntergeladen werden.
        repositories {
            flatDir {
                dirs '/home/gradle/libs'
            }
        }
    }
}
